
datasets ,= glob_wildcards("input/{dataset}.fasta")
genomes ,= glob_wildcards("genomes/{genome}.fna")
genes = {
    'nifH': [ "reviewed:true", "xref:interpro-IPR005977", "taxonomy_id:1117" ],
    'hetR': [ "reviewed:true", "xref:interpro-IPR005319", "taxonomy_id:1117" ],
    'patS': [ "accession:O52748" ],
    'hetN': [ "accession:P37694" ],
    'GvTcR': [ "accession:Q7NKP0" ]
}

databases, bases ,= glob_wildcards("databases/{database}/{base}.fna.ndb")

include: "Pgap.snakefile"

rule all:
    input:
        #expand("analysis/blastp/{gene}/{genome}.txt", gene = genes, genome = genomes),
        #expand("analysis/genomes/{genome}.faa", genome = genomes),
        expand("analysis/tblastn/{database}/{base}_xrs.faa", zip, database = databases, base = bases)

rule cat_sequences:
    input:
        "input/cyanobacteria.fasta",
        "input/XRs.fasta"
        # "input/outgroups.fasta"
    output:
        "analysis/sequences.fasta"
    params:
        m = 220
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -m {params.m} -o {output} {input}"

rule cdhit:
    input:
        "analysis/sequences.fasta"
    output:
        fasta = "analysis/sequences.fasta.cdhit",
        clstr = "analysis/sequences.fasta.cdhit.clstr"
    params:
        c = 1
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output.fasta} -c {params.c} -d 0"

rule mafft:
    input:
        "analysis/sequences.fasta.cdhit"
    output:
        "analysis/sequences.fasta.mafft"
    conda:
        "envs/mafft.yaml"
    shell:
        "mafft --auto --reorder {input} > {output}"

rule trimal:
    input:
        "analysis/sequences.fasta.mafft"
    output:
        "analysis/sequences.fasta.trimal"
    conda:
        "envs/trimal.yaml"
    shell:
        "trimal -in {input} -out {output} -automated1"

rule raxml:
    input:
        "analysis/sequences.fasta.trimal"
    output:
        "analysis/RAxML_info.txt",
        "analysis/RAxML_bipartitions.txt",
        "analysis/RAxML_bestTree.txt"
    params:
        model = "PROTGAMMAAUTO",
        seed = 123,
        bootstrap = 1000
    conda:
        "envs/raxml.yaml"
    threads:
        20
    shell:
        "raxmlHPC-PTHREADS-SSE3 -f a -p {params.seed} -x {params.seed} -# {params.bootstrap} -m {params.model} -T {threads} -s {input} -n txt -w $(dirname $(realpath {output}))"

rule plot_tree:
    input:
        tree = "analysis/RAxML_bipartitions.txt",
        metadata = "input/metadata.xlsx",

    output:
        "analysis/RAxML.svg"
    params:
        root_by = [ "KIE48989.1", "KGA12121.1" ]
    script:
        "scripts/plot_tree.R"

rule gtdbtk:
    input:
        "genomes"
    output:
        directory("analysis/gtdbtk")
    params:
        data_path = "/data/gtdbtk/release214.1/auxillary_files/gtdbtk_r214_data/release214"
    conda:
        "envs/gtdbt.yaml"
    shell:
        "GTDBTK_DATA_PATH='{params.data_path}' gtdbtk classify_wf --genome_dir {input} --out_dir {output} --skip_ani_screen"

rule dload_gene_reps:
    output:
        "analysis/genes/{gene}.fasta"
    params:
        query = lambda w: '+AND+'.join(genes[w.gene])
    shell:
        "wget -qO {output} 'https://rest.uniprot.org/uniprotkb/stream?download=true&format=fasta&query={params.query}'"

rule copy_genome:
    input:
        "genomes/{genome}.fna"
    output:
        "analysis/genomes/{genome}.fna"
    shell:
        "cp {input} {output}"

rule makeblast_nucl:
    input:
        "analysis/genomes/{genome}.fna"
    output:
        "analysis/genomes/{genome}.fna.ndb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype nucl"

rule makeblast_prot:
    input:
        "{prefix}.faa"
    output:
        "{prefix}.faa.pdb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype prot"

rule blastp:
    input:
        query = "analysis/genes/{gene}.fasta",
        db = "analysis/genomes/{genome}.faa",
        pdb = "analysis/genomes/{genome}.faa.pdb"
    output:
        "analysis/blastp/{gene}/{genome}.txt"
    params:
        fmt = 'qseqid sseqid pident length mismatch gapopen qstart qend sstart send evalue bitscore qseq sseq'
    conda:
        "envs/blast.yaml"
    shell:
        "blastp -db {input.db} -query {input.query} -outfmt '6 {params.fmt}' -out {output}"

rule tblastn:
    input:
        query = "analysis/refs/proteins/XR.faa",
        ndb = "databases/{database}/{base}.fna.ndb"
    output:
        "analysis/tblastn/{database}/{base}.txt"
    params:
        db = lambda w, input: os.path.splitext(input.ndb)[0]
    conda:
        "envs/blast.yaml"
    threads:
        20
    shell:
        "tblastn -num_threads {threads} -db {params.db} -query {input.query} -outfmt 6 -out {output} -max_target_seqs 1000000000"

rule extract_scaffolds:
    input:
        tblastn = "analysis/tblastn/{database}/{base}.txt",
        ndb = "databases/{database}/{base}.fna.ndb"
    output:
        "analysis/tblastn/{database}/{base}.fna"
    params:
        db = lambda w, input: os.path.splitext(input.ndb)[0],
        max_e = 1e-10,
        min_len = 200
    conda:
        "envs/blast.yaml"
    shell:
        "awk -v E={params.max_e} -v L={params.min_len} '$11<E && $4>=L' {input.tblastn} | cut -f2 | sort -u | blastdbcmd -db {params.db} -entry_batch - > {output}"

rule scaffold_headers:
    input:
        expand("analysis/tblastn/{db}/{base}.fna", zip, db = databases, base = bases)
    output:
        "analysis/tblastn/scaffold_headers.txt"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -n {input} | awk '!_[$1];{{_[$1]=1}}' > {output}"

rule prodigal_scaffolds:
    input:
        "analysis/tblastn/{database}/{base}.fna"
    output:
        gff = "analysis/tblastn/{database}/{base}.gff",
        faa = "analysis/tblastn/{database}/{base}.faa"
    conda:
        "envs/prodigal.yaml"
    shell:
        "prodigal -i {input} -f gff -o {output.gff} -a {output.faa} -p meta"

rule copy_superclade_d:
    input:
        "input/Superclade_P.faa"
    output:
        "analysis/refs/Superclade_P.faa"
    shell:
        "cp {input} {output}"

rule extract_protein:
    input:
        "input/ingroup.fasta"
    output:
        "analysis/refs/proteins/{protein}.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit grep -p {wildcards.protein} {input} | seqkit seq -i -o {output}"

rule blast_superclade_p:
    input:
        query = "analysis/tblastn/{database}/{base}.faa",
        db = "analysis/refs/Superclade_P.faa",
        pdb = "analysis/refs/Superclade_P.faa.pdb"
    output:
        "analysis/tblastn/{database}/{base}_superclade_p.txt"
    params:
        outfmt = 'qseqid sseqid pident length mismatch gapopen qstart qend sstart send evalue bitscore stitle',
        max_target_seqs = 5,
        evalue = 1e-10
    conda:
        "envs/blast.yaml"
    threads:
        20
    shell:
        "blastp -query {input.query} -db {input.db} -out {output} -outfmt '6 {params.outfmt}' -max_target_seqs {params.max_target_seqs} -evalue {params.evalue} -num_threads {threads}"

rule blast_superclade_p_extract_xrs:
    input:
        blast = "analysis/tblastn/{database}/{base}_superclade_p.txt",
        faa = "analysis/tblastn/{database}/{base}.faa"
    output:
        "analysis/tblastn/{database}/{base}_xrs.faa"
    params:
        clade = '/P:XR/',
        id = 40,
        evalue = 1e-30
    conda:
        "envs/kits.yaml"
    shell:
        "awk -vc={params.clade} -F\\\\t '!_[$1] && $13~c && $11<{params.evalue} && $3>={params.id} {{print$1}}; {{_[$1]=1}}' {input.blast} | seqkit grep -f- {input.faa} -o {output}"

rule cat_proteins:
    input:
        "input/ingroup.fasta",
        "input/outgroups.fasta",
        expand("analysis/tblastn/{database}/{base}_xrs.faa", zip, database = databases, base = bases)
    output:
        "analysis/proteins/big.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit rmdup -o {output} {input}"

rule cdhit_xrs:
    input:
        "analysis/proteins/big.faa"
    output:
        fasta = "analysis/proteins/big_cdhit.faa",
        clstr = "analysis/proteins/big_cdhit.faa.clstr"
    params:
        c = 0.95
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output.fasta} -c {params.c} -d 0"

rule cdhit_select_proteins:
    input:
        refs = "input/ingroup.fasta",
        fasta = "analysis/proteins/big.faa",
        clstr = "analysis/proteins/big_cdhit.faa.clstr"
    output:
        "analysis/proteins/big_cdhit_selected.faa"
    conda:
        "envs/python.yaml"
    script:
        "scripts/cdhit_select.py"

rule mafft_xrs:
    input:
        "analysis/proteins/big_cdhit_selected.faa"
    output:
        "analysis/proteins/big_cdhit.mafft"
    conda:
        "envs/mafft.yaml"
    threads:
        20
    shell:
        "mafft --thread {threads} --reorder --auto {input} > {output}"

rule trimal_xrs:
    input:
        "analysis/proteins/big_cdhit.mafft"
    output:
        "analysis/proteins/big_cdhit.trimal"
    params:
        gt = 0.9
    conda:
        "envs/trimal.yaml"
    shell:
        "trimal -in {input} -out {output} -gt {params.gt} -keepheader"

rule gene_iqtree:
    input:
        "analysis/proteins/big_cdhit.trimal"
    output:
        expand("analysis/proteins/big_cdhit.{ext}", ext = [ "treefile", "model.gz", "iqtree", "ckp.gz" ])
    params:
        seed = 123,
        B = 1000,
        prefix = "analysis/proteins/big_cdhit"
    threads:
        4
    conda:
        "envs/iqtree.yaml"
    shell:
        "iqtree2 -s {input} --prefix {params.prefix} -redo --alrt 1000 -B {params.B} --seed {params.seed} -T {threads}"

rule tblastn_gene:
    input:
        query = "analysis/genes/{gene}.fasta",
        db = "analysis/genomes/{genome}.fna",
        ndb = "analysis/genomes/{genome}.fna.ndb"
    output:
        "analysis/tblastn_gene/{gene}/{genome}.txt"
    params:
        fmt = 'qseqid sseqid pident length mismatch gapopen qstart qend sstart send evalue bitscore sseq'
    conda:
        "envs/blast.yaml"
    shell:
        "tblastn -db {input.db} -query {input.query} -outfmt '6 {params.fmt}' -out {output}"

rule prodigal:
    input:
        "analysis/genomes/{genome}.fna"
    output:
        gff = "analysis/genomes/{genome}.gff",
        faa = "analysis/genomes/{genome}.faa"
    conda:
        "envs/prodigal.yaml"
    shell:
        "prodigal -i {input} -f gff -o {output.gff} -a {output.faa}"
