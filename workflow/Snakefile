
datasets ,= glob_wildcards("input/{dataset}.fasta")
genomes ,= glob_wildcards("genomes/{genome}.fna")

genes = {
    # 'nifH': [ "reviewed:true", "xref:interpro-IPR005977", "taxonomy_id:1117" ],
    'GvTcR': [ "accession:Q7NKP0" ],
    'Diox1': [ "accession:P74334 OR accession:Q8YPB4" ],
}

databases, bases ,= glob_wildcards("databases/{database}/{base}.fna.ndb")

trees = {
    'GTDB': 'https://data.gtdb.ecogenomic.org/releases/release214/214.1/bac120_r214.tree',
    'GEM': 'https://data.gtdb.ecogenomic.org/releases/release89/89.0/bac120_r89.tree'
}
taxonomies = {
    'GTDB': 'https://data.gtdb.ecogenomic.org/releases/release214/214.1/bac120_taxonomy_r214.tsv',
    'GEM': 'https://data.gtdb.ecogenomic.org/releases/release89/89.0/bac120_taxonomy_r89.tsv'
}

profile = 'PF01036'
profiles = [ 'PF03055', 'PF15461', 'PF00502' ]

include: "Cyanobacterial_clade.snakefile"
include: "Cyanobacterial_XR_genomes.snakefile"
include: "Global_XR_phylogeny.snakefile"
include: "Rhodopsins_in_cyanobacteria.snakefile"

rule all:
    input:
        "output/Cyanobacterial_XR_clade.svg",
        "output/All_prokaryotic_XRs.svg",
        expand("output/{gene}_homologs.fasta", gene = genes),
        expand(expand("analysis/hmmsearch/{database}/{base}_{{profile}}.txt", zip, database = databases, base = bases), profile = profiles),
        expand("output/Rhodopsins_in_cyanobacteria_{database}-{base}.svg", zip, database = databases, base = bases)

# Make blast database for a protein fasta
rule makeblast_prot:
    input:
        "{prefix}.faa"
    output:
        "{prefix}.faa.pdb"
    conda:
        "envs/search.yaml"
    shell:
        "makeblastdb -in {input} -dbtype prot"

# Download Pfam profile
rule dload_pfam:
    output:
        "analysis/pfam/{profile}.hmm"
    params:
        url = "https://www.ebi.ac.uk/interpro/wwwapi/entry/pfam/{profile}?annotation=hmm"
    shell:
        "wget -O- {params.url:q} | gzip -cd > {output}"

# Copy the rhodopsin list (rhodopsins from families present in prokaryotes)
rule copy_rhodopsin_list:
    input:
        "databases/Rhodopsin_list.faa"
    output:
        "analysis/rhodopsin_list/Rhodopsin_list.faa"
    shell:
        "cp {input} {output}"
